
abstract module Organ(super.length) extends M(length)
{	
	int rank;				// phytomer rank
	int cpn;				// cumulative phytomer number
	int order;				// order (0 = main stem)
	double age;				// organ age (dd)	
	
	
	// calculate age for EACH organ
	void calcAge() 
	{	
			age++;
	}	
}

module SoilBase{
	
	double dimX = 1;
	double dimY = 1;
	double Depth_Plant = 0.25;
	int plantN = 1;

}
==> RL (90) Translate(0,Depth_Plant,-dimX/2) Parallelogram(dimX,dimY).(setColor(0.51,0.33,0.16))
;

module Seed extends Null
{
	double diameter;
	double 	age;
		
	boolean shouldInit()
	{
		if(age > 1)
		{
			return true;
		} else
		{
			return false;
		}
	}	
}
==> Translate(0,0,0) Sphere(diameter).(setShader(YELLOW))
;


module Roots extends Organ
{
	double depth;
	double radius;
	
	void initiate(){
	
		age = 0;
		depth = 0;
		radius = 0;
		length = 0;
	
	}
	
	void getDepth(){
		double dm = 1.;
		double rm = 0.5;
		double am = 10;
		double k = 0.5;
		
		depth = dm / (1 + Math.exp(-k*(age-am)));
		radius = rm / (1 + Math.exp(-k*(age-am)));		
		
	}
	
}
==>  Translate(0,0,0) Rotate(180,0,0) Cone(depth,radius).(setColor(0.3,0.33,0.16))
;

module Apex extends Organ
{		
	double diameter;	
	double plas = 1;
	double translation;
	int live_rank;
	
	boolean shouldProduce()
	{
		if(age > (rank * plas))
		{
			return true;
		} else
		{
			return false;
		}
	}
	
	void increment(){
	rank = rank +1;
	}
	
	void initiate(){
				
		age		= 0;
		order	= 0;
		diameter = 0.015;
		
	}
	
	void initiateAxillary(Apex a){
			
		rank	= a.rank;
		age		= 0;
		order	= a.order+1;
		diameter = 0.005;
		
	}
	
	void UpdatePos(){
		if(order>0){
		Internode it = selectWhere((*i:Internode*), i.rank == rank);
		translation = it.diam/2;
		}
	}
	
}
//Draw bud only when be 4th live_rank
==> if(order > 0 && live_rank > 4) (Translate(0,-0.005,translation)) Sphere(diameter/2).(setShader(RED))
;

module Internode extends Organ//extends Cylinder.(setShader(YELLOW))
{
	
	double diam;
	double am 	= 7;
	double lm 	= 0.15;
	double dm	= 0.05;
	double k	= 1.0;
	double Height;
	int live_rank;
	
	
	void initiate(Apex a){
		
		age = 0;
		rank = a.rank;
		order = a.order;
		length = 0;
		diam = a.diameter;
		
		
	}	
	
	void Growth(){		
		
		diam = (dm) / (1 + Math.exp(-k*(age-am)));
		
		// Checks the Height of this internode
		Height = sum((* i:Internode, (i.rank < rank) *)[length]) + sum((* n:NodeInt, (n.rank < rank) *)[length]);
		SoilBase sb = selectWhere((*s:SoilBase*), s.plantN == 1);
		
		// If this internode reached the soil surface, increase the maximum length
		if(sb.Depth_Plant > Height) { lm = 0.05;}else{lm = 0.15;}		
		length = lm / (1 + Math.exp(-k*(age-am)));
		
	}
	
	
}
==> Cylinder(length,diam/2).(setShader(YELLOW))
;


module NodeInt extends Organ
{
	double diameter;
	double am 	= 7;
	double lm 	= 0.01;
	double k	= 1.0;
	
	void initiate(Apex a){
		
		diameter = a.diameter;
		rank = a.rank;
		order = a.order;
		age = 0;
		length = 0;
		
	}
	
	void getDiameter(){
		Internode it = selectWhere((*i:Internode*), i.rank == rank);
		diameter = it.diam;
		length = lm / (1 + Math.exp(-k*(age-am)));
		
	}
}
==> Cylinder(length,diameter/2).(setShader(BLACK))
;

module Leaf extends Organ
{
	
	int w_ratio = 25;
	double lm = 1.5;
	double blade_len;
	double width;
	double thickness;
	double petlength;
	double petdiameter;
	double leafangle;
	int live_rank;
	int MaxLeaves = 10;
	
	void initiate(Apex a){
	
		age = 0;
		rank = a.rank;
		order = a.order;
		live_rank = 0;
		thickness = 0.002;
		length = 0;
		width = 0;
		
		petdiameter = a.diameter + thickness * 2;
				
	}
	
	void Growth()
	{
		double am 	= 5;
		double k	= 0.5;
		
		length = lm / (1 + Math.exp(-k*(age-am)));
		blade_len = length;		
		width = length / w_ratio;
		
		Internode it = selectWhere((*i:Internode*), i.rank == rank);		
		petdiameter = (it.diam) + thickness * 2;
		
		SoilBase sb = selectWhere((*s:SoilBase*), s.plantN == 1);
		//sb.Depth_Plant
		
		petlength = sb.Depth_Plant  + it.length;
		
		double k_angle	= 1;
		double am_angle	= 6;
		double max_angle = 60;
		if(rank < 4) {am_angle = 2;}
		leafangle  = max_angle / (1 + Math.exp(-k_angle*(age-am_angle))); 
		
	}		
	
}
==> Cylinder(petlength, (petdiameter/2)).(setShader(GREEN)) RL(leafangle) Box(blade_len, width, thickness).(setShader(GREEN))
;


protected void init()
[
	Axiom ==> 	
	
	b:SoilBase
	s:Seed
	{
		s.diameter	= 0.005;
		s.age 		= 0.0;
	};
]

public void run()
{	
[
	// Seed Germination
	s:Seed, (s.shouldInit()) 
	==> r:Roots a:Apex 
	{
		r.initiate(); 
		a.initiate();
	};
	
		
	// Produce vegetative phytometer
	a:Apex, (a.shouldProduce() && a.order ==0)
	==>  n:NodeInt [RL(90) b:Apex] [RH(180) l:Leaf] i:Internode RH(180) a
	{
		n.initiate(a);
			b.initiateAxillary(a);
			l.initiate(a);
		i.initiate(a);
		a.increment();		
		
	};	
	
	s:Seed ::>
	{
		s.age++;
	}
	
	r:Roots ::>
	{
		r.getDepth();
		r.age++;
	}
	
	// adding/changing attributes to created objetcs
	a:Apex ::> 
	{
		a.age++;
		a.UpdatePos();
		if(a.order ==1) {a.live_rank++;}
	}
	
	n:NodeInt ::> 
	{
		n.age++;
		n.getDiameter();
	}
	
	i:Internode ::> 
	{
		i.age++;		
		i.Growth();
		i.live_rank++;
		
	}
	
	l:Leaf ::> 
	{
		l.age++;
		l.Growth();	
		l.live_rank++;
		
	}
	
	l:Leaf, (l.live_rank > l.MaxLeaves) ==>> ; 

]

}
